apply plugin: 'com.android.library'
// 添加插件
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 14
        versionCode 100
        versionName "1.0.0"
    }

    // 使用 JDK 1.8
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        // 剔除这个包下的所有文件（不会移除签名信息）
        exclude 'META-INF/*******'
    }

    android.libraryVariants.all { variant ->
        // aar 输出文件名配置
        variant.outputs.all { output ->
            outputFileName = "${rootProject.name}-${android.defaultConfig.versionName}.aar"
        }
    }
}

afterEvaluate {
    // 排除 BuildConfig.class 和 R.class
    generateReleaseBuildConfig.enabled = false
    generateDebugBuildConfig.enabled = false
    generateReleaseResValues.enabled = false
    generateDebugResValues.enabled = false
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}


task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// 生成jar包的task
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}


group = "io.github.orixent"// groupId
version = "1.0.0"// 库的当前版本

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonaUsername, password: sonaPassword)
            }


            pom.project {
                name 'quick-toast'// 项目名称
                packaging 'aar'// 资源类型
                artifactId 'quick-toast'// 库名
                description 'An android toast util'// 描述
                url 'https://github.com/Orixent/QuickToast'
                scm {
                    connection = 'https://github.com/Orixent/QuickToast.git'
                    developerConnection = 'https://github.com/Orixent/QuickToast.git'
                    url = 'https://github.com/Orixent/QuickToast'
                }


                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'orixent'
                        name = 'Orixent'
                        email = 'orixent@163.com'
                    }
                }
            }
        }
    }
}
// 签名
signing {
    sign configurations.archives
}